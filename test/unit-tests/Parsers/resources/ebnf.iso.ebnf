"EBNF defined in itself." {
  syntax            = [ syntax_title ] , '{' , {COMMENT}, { rule , COMMENT } , '}' , [ syntax_comment ] .
  syntax_title      = STRING_LITERAL .
  syntax_comment    = STRING_LITERAL .

  rule              = identifier , ( '=' | ':' | ':==' ) , expression , ( '.' | ';' ) .
  expression        = term , { '|' , term } .
  term              = factor, { ',' , factor } .
  factor            = IDENTIFIER
                    | STRING_LITERAL
                    | SPECIAL_SEQUENCE
                    | optional_expression
                    | grouped_expression
                    | repeated_expression .

  optional_expression = '[' , expression , ']' .
  grouped_expression  = '(' , expression , ')' .
  repeated_expression  = '{' , expression , '}' .

  (* LEXER RULES *)

  IDENTIFIER        = LETTER , {'_' | LETTER | DECIMAL_DIGIT} .

  LETTER            = '\p{L}' .

  DECIMAL_DIGIT     = '[0-9]' .

  STRING_LITERAL    = "'[^']*'" | '"[^"]*"' .

  SPECIAL_SEQUENCE  = '?[^?]*?' .

  COMMENT           = '\(\*\s*\*\)|\(\*(?!\*\))(?:.|\n|\r)*?\*\)' .

} "For more information see http://standards.iso.org/ittf/PubliclyAvailableStandards/s026153_ISO_IEC_14977_1996(E).zip"
